#use-added-syntax(jitx)
defpackage BQ51003YFPR :
  import core
  import collections
  import math
  import jitx
  import jitx/commands

  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/bundles
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components

pcb-landpattern lp :
  make-bga-landpattern(7, 4, 0.400, 0.250, typ(1.6), typ(2.8))
  model3d = Model3D("3d-models/BQ51003YFPR--3DModel-STEP-56544.STEP",
    Vec3D(0.0, 0.0, 0.0),
    Vec3D(1.0, 1.0, 1.0),
    Vec3D(-90.0, 0.0, 0.0))

public pcb-component component :
  name = "BQ51003YFPR"
  mpn = "BQ51003YFPR"
  manufacturer = "Texas Instruments"
  description = "IC WIRELESS PWR RCVR 28DSBGA"
  datasheet = "https://www.ti.com/lit/gpn/bq51003"
  pin-properties :
    [ pin : Ref | pads : Ref ...       | side:Dir ]    
    [  AC1      |  B[3] B[4]           | Right ]
    [  AC2      |  B[1] B[2]           | Right ]
    [  AD       |  G[4]                | Left  ]
    [  nAD-EN   |  F[3]                | Right ]
    [  BOOT1    |  C[4]                | Up    ]
    [  BOOT2    |  C[1]                | Up    ]
    [  nCHG     |  F[4]                | Right ]
    [  CLAMP1   |  E[3]                | Right ]
    [  CLAMP2   |  E[2]                | Right ]
    [  COMM1    |  E[4]                | Right ]
    [  COMM2    |  E[1]                | Right ]
    [  EN1      |  G[3]                | Left  ]
    [  EN2      |  G[2]                | Left  ]
    [  FOD      |  F[2]                | Left  ]
    [  ILIM     |  G[1]                | Left  ]
    [  OUT      |  D[1] D[2] D[3] D[4] | Right ]
    [  PGND     |  A[1] A[2] A[3] A[4] | Down  ]
    [  RECT     |  C[2] C[3]           | Down  ]
    [  TS-CTRL  |  F[1]                | Left  ]

  make-box-symbol()
  assign-landpattern(lp)

; pcb-landpattern ant-qi-coil :

;   pad p[1] : smd-pad(0.5, 0.5) at loc(0.0, 0.25)
;   pad p[2] : smd-pad(0.9, 0.5) at loc(-2.1, 0.25)

;   copper(LayerIndex(0)) = Line(0.5, [Point(-2.1, 5.15), Point(2.2, 5.15), Point(2.2, 2.51), 
;                                 Point(4.7, 2.51),  Point(4.7, 5.15), Point(6.9, 5.15), 
;                                 Point(6.9, 2.51),  Point(9.4, 2.51), Point(9.4, 5.15), 
;                                 Point(11.6, 5.15), Point(11.6, 0.96)])
;   copper(LayerIndex(0)) = Line(0.5, [Point(0.0, 0.0),   Point(0.0, 5.15)])
;   copper(LayerIndex(0)) = Rectangle(0.9, 5.4, loc(-2.1, 2.7))
;   layer(ForbidCopper(LayerIndex(0), LayerIndex(0, Bottom))) = Rectangle(30.0, 5.5, loc(5.0, 3.25))
;   layer(SolderMask(Top)) = Rectangle(30.0, 5.5, loc(5.0, 3.25))
;   layer(Courtyard(Top)) = Rectangle(30.0, 5.5, loc(5.0, 3.25))
;   layer(Courtyard(Bottom)) = Rectangle(30.0, 5.5, loc(5.0, 3.25))
;   ref-label()

; pcb-component qi-antenna-cmp :
;   port p : pin[[1 2]]
;   description = "Qi wireless charging antenna"
;   val pkg = ant-qi-coil
;   val sym = antenna-symbol(1, 0)
;   landpattern = pkg(p[1] => pkg.p[1], p[2] => pkg.p[2])
;   symbol = sym(p[1] => sym.p[1], p[2] => sym.p[2])
;   reference-prefix = "ANT"
;   property(self.rated-temperature) = false

public pcb-module module :
  pin VBUS
  pin VBUSp
  pin GND
  pin EN1
  pin EN2
  pin nCHG
  pin CTRL

  public inst wpwr : BQ51003YFPR/component

  inst coil : pin-header(2, 1.000)
  component-status(coil) :
    bom-status = NotInBOM

  net (nCHG wpwr.nCHG)
  net (CTRL wpwr.TS-CTRL)
  net (EN1 wpwr.EN1)
  net (EN2 wpwr.EN2)
  net (VBUS wpwr.OUT)
  net (GND wpwr.PGND)

  bypass-cap-strap(wpwr.OUT GND 2.2e-6)


  res-strap(wpwr.ILIM wpwr.FOD 845.0)
  res-strap(wpwr.FOD GND 196.0)
  res-strap(wpwr.FOD wpwr.RECT 20.0e3)

  res-strap(wpwr.EN1 GND 10.0e3)
  res-strap(wpwr.EN2 GND 10.0e3)
  res-strap(wpwr.TS-CTRL GND 10.0e3)
  res-strap(wpwr.AD GND 1.0e3)

  bypass-cap-strap(wpwr.CLAMP1 wpwr.AC1 470.0e-9)
  bypass-cap-strap(wpwr.CLAMP2 wpwr.AC2 470.0e-9)

  bypass-cap-strap(wpwr.COMM1 wpwr.AC1 22.0e-9)
  bypass-cap-strap(wpwr.COMM2 wpwr.AC2 22.0e-9)

  bypass-cap-strap(wpwr.BOOT1 wpwr.AC1 10.0e-9)
  bypass-cap-strap(wpwr.BOOT2 wpwr.AC2 10.0e-9)

  bypass-cap-strap(wpwr.AC1, wpwr.AC2, 270.0e-12)

  bypass-cap-strap(wpwr.AC1 coil.p[1] 10.0e-9)
  net (coil.p[2] wpwr.AC2)

  bypass-cap-strap(wpwr.RECT, GND, 10.0e-6)
  bypass-cap-strap(wpwr.RECT, GND, 4.7e-6)

  inst q1 : CSD75207W15/component 
  ; database-part(["mpn" => "CSD75207W15", "manufacturer" => "Texas Instruments"])

  net (VBUSp q1.G1 q1.D10 q1.D11)
  net (VBUS q1.D20 q1.D21 q1.D22)
  net (wpwr.nAD-EN q1.D12 q1.S2)

  ; res-strap(VBUSp wpwr.AD, 0.0)
  bypass-cap-strap(VBUSp, GND, 4.7e-6)

  schematic-group(self) = wireless-power
  layout-group(self) = wireless-power
